if (project.isRunAlone.toBoolean()) {
    apply plugin: "com.android.application"
} else {
    apply plugin: "com.android.library"
}
apply from: "../../basic.gradle"

android {
    defaultConfig {
        if (project.isRunAlone.toBoolean()) {
            applicationId rootProject.ext.android.applicationBaseId + project.getName()
            multiDexEnabled true
        }
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildFeatures {
        // Enables Jetpack Compose for this module
        compose true
        dataBinding true
    }
    resourcePrefix project.getName() + "_"

    composeOptions {
        kotlinCompilerVersion "1.3.70-dev-withExperimentalGoogleExtensions-20200424"
        kotlinCompilerExtensionVersion "$compose_version"
    }

    sourceSets {
        main {
            // 单独调试与集成调试时使用不同的 AndroidManifest.xml 文件
            if (project.isRunAlone.toBoolean()) {
                manifest.srcFile 'src/main/manifest/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
            }
        }
    }
}

dependencies {
    api project(':libs:base')
//    api project(':libs:ext')
    api project(':store:wanandroid')

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}